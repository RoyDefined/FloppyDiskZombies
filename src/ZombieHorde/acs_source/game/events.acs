// These cvars are set on map load to determine if they must be set manually.
// When the map unloads, these cvars are unset to restore the original context.
bool _shootThroughAlliesCvarEnabled;
bool _clientSendFullButtonInfoCvarEnabled;
bool _dontStopPlayerScriptsOnDisconnectCvarEnabled;

script "OnOpen" open
{
	_shootThroughAlliesCvarEnabled = GetCvar("sv_shootthroughallies");
	_clientSendFullButtonInfoCvarEnabled = GetCvar("compat_clientssendfullbuttoninfo");
	_dontStopPlayerScriptsOnDisconnectCvarEnabled = GetCvar("compat_dont_stop_player_scripts_on_disconnect");
	
	if (!_shootThroughAlliesCvarEnabled) {
		SetGameplaySetting("sv_shootthroughallies", true);
	}
	
	if (!_clientSendFullButtonInfoCvarEnabled) {
		SetGameplaySetting("compat_clientssendfullbuttoninfo", true);
	}
	
	if (!_dontStopPlayerScriptsOnDisconnectCvarEnabled) {
		SetGameplaySetting("compat_dont_stop_player_scripts_on_disconnect", true);
	}
}

script "OnUnloading" unloading
{
	if (!_shootThroughAlliesCvarEnabled) {
		SetGameplaySetting("sv_shootthroughallies", false);
	}
	
	if (!_clientSendFullButtonInfoCvarEnabled) {
		SetGameplaySetting("compat_clientssendfullbuttoninfo", false);
	}
	
	if (!_dontStopPlayerScriptsOnDisconnectCvarEnabled) {
		SetGameplaySetting("compat_dont_stop_player_scripts_on_disconnect", false);
	}
}

script "OnEnter" enter
{
	AssignPlayerTID();
	ClearInventory();
	GiveHumanWeapons();
	SetActorProperty(0, APROP_DamageFactor, 0.0);
	
	GiveInventory("PlayerSpeedModifier", 1);
	
	int failCount;
	if (!ACS_NamedExecuteAlways("WeaponLoop", 0)) {
		LogError(StrParam(s:"Weapon loop could not be started for ", s:PlayerName(PlayerNumber()), s:"."));
	}
	
	if (!NamedExecuteClientScript("WeaponLoopClient", PlayerNumber())) {
		LogError(StrParam(s:"Client weapon loop could not be started for ", s:PlayerName(PlayerNumber()), s:"."));
	}
}

script "ClientOpen" open clientside
{
	ACS_NamedExecute("DrawMapInfo", 0);
	
	if (!ACS_NamedExecute("ClientsideLoop", 0)) {
		LogError("Client player loop could not be started.");
	}
}

script "OnDisconnect" (int id) disconnect
{
	RemovePlayer(id);
}

script "OnDeath" death
{
	Thing_ChangeTID(0, 0);
	RemovePlayer(PlayerNumber());
	ClearInventory();
}

script "DrawMapInfo" (void) clientside
{
	str levelName = StrParam(n:PRINTNAME_LEVELNAME);
	int mapInfoHorizontal = MAPINFOX,
		mapInfoVertical = MAPINFOY;
	
	SetHudSize(960, 600, TRUE);
	SetFont("BigFont");
	HudMessage(s:"\cgZombie Horde Lite \cj- ", s:levelName; HUDMSG_TYPEON, 0, CR_UNTRANSLATED, mapInfoHorizontal, mapInfoVertical, 6.0, 0.05, 0.5);
	
	delay(35*2);
	
	SetHudSize(960, 600, TRUE);
	SetFont("SmallFont");
	HudMessage(s:"By FusedQyou"; HUDMSG_FADEINOUT, 0, CR_WHITE, mapInfoHorizontal, mapInfoVertical + 13.0, 4.0, 1.0, 0.5);
}