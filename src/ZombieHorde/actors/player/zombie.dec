actor ZombiePlayer : PlayerPawn
{
	player.forwardmove 0.8
	player.sidemove 0.4
	Speed 1.25
	
	// Value is determined upon infection.
	// But this value can stay the same when you change to a zombie through debugging etc.
	Health 1000
	
    Player.MaxHealth 5000
	Radius 16
    Height 56
	PainChance 100
	Mass 40
	
	Player.DisplayName "Zombie"
    Player.MorphWeapon "ZombieClaws"
	+CROUCHABLEMORPH 
    +NOSKIN 
	
	States
	{
		Spawn:
			INCA A -1
			loop
			
		See:
			INCA ABCDEF 5
			loop
			
		Melee:
		Missile:
			TNT1 A 0 A_Jump(128, "FireRight")
		
		FireLeft:
			INCA G 3
			INCA H 2
			goto Spawn
			
		FireRight:
			INCA R 3
			INCA S 2
			goto Spawn
			
		Pain:
			INCA L 8 A_PlaySound("player/ZOMBIEHIT")
			goto Spawn
			
		Death:
		XDeath:
		AltSkinDeath:
		AltSkinXDeath:
			INCA M 3
			INCA N 3
			INCA N 3 A_PlaySound("player/ZOMBIEDIE")
			INCA O 3 A_NoBlocking
			INCA PQ 3
			TNT1 A -1 A_SpawnItem("ZombieCorpseFader", 0, 0, 0, true)
	}
}

// Zombie corpse fader
actor ZombieCorpseFader
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	
	var int user_scale;
	states
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetTics(random(70, 140))
			TNT1 A 0 A_SetUserVar("user_scale", user_scale + 1)
			INCA Q 1 A_SetScale(1.0 - 0.01*user_scale, 1.0 - 0.02*user_scale)
			TNT1 A 0 A_FadeOut(.02)
			goto Spawn+1
	}
}