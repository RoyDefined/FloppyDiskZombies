actor GrenadeFlameEffect
{
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	+DONTSPLASH
	+NOTIMEFREEZE
	+NOBLOCKMAP
	+BRIGHT
	
	Renderstyle Add
}

actor GrenadeFlameEffectNoGravity : GrenadeFlameEffect { +NOGRAVITY }

actor GrenadeExplosion : GrenadeFlameEffect
{
	Scale 2
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay Radius_Quake(4, 30, 0, 9, 0)
			GXP1 AB 1
			TNT1 AAAAAA 0 A_SpawnItemEx("GrenadeFlamePuff",0, 0, 0, random(-8, 8), random(-8, 8), random(0, 4), random(0, 359))
			TNT1 AAAAAA 0 A_SpawnItemEx("GrenadeFlamePuffHuge",0, 0, 0, random(-8, 8), random(-8, 8), random(0, 4), random(0, 359))
			GXP1 CDEFGHI 1
			TNT1 AAAAAAAAAA 0 A_SpawnItemEx("GrenadeSmoke", random(-3, 3), random(-3, 3), random(10, 20), Frandom(-1.2, 1.2), Frandom(-1.2, 1.2), Frandom(1.0, 4.0), random(0, 359))
			TNT1 AAAAAA 0 A_SpawnItemEx("GrenadeSmoke", random(-3, 3), random(-3, 3), random(10, 20), Frandom(-0.8, 0.8), Frandom(-0.8, 0.8), Frandom(0.1, 1.0), random(0, 359))
			GXP1 J 1
			stop
	}
}

actor GrenadeSmoke
{
	+Missile
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NOTIMEFREEZE
	+FORCEXYBILLBOARD
	
	Alpha 0.4
	RenderStyle Translucent
	States
	{
		Spawn:
			TNT1 A 0 A_SetScale(Frandom(0.4, 0.8), Frandom(0.4, 0.8))
			SMKD A 1 A_FadeOut(0.01)
			wait
	}
}

actor GrenadeFlamePuff : GrenadeFlameEffectNoGravity
{
	States
	{
		Spawn:
			FIR1 ABCDABCDABCDEFGHI 1
			stop
			
		NoSpawn:
			TNT1 A 0
			stop
	}
}
actor GrenadeFlamePuffHuge : GrenadeFlamePuff 			 { Scale 2.0 }