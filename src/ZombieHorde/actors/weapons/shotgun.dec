actor ShotgunShell : Ammo { Inventory.MaxAmount 8 }
actor ShotgunPump  : Boolean {}

actor ShotgunSpawn : ZHweaponSpawn 8000
{
	Inventory.PickupMessage "Hunting shotgun"
	Tag "Hunting shotgun"
	
	States
	{
		// Spawn imitates a stock shotgun, because you're not supposed to see it anyway.
		Spawn:
			SHOT A -1
		
		Pickup:
			TNT1 A 0 A_JumpIf(CallACS("CheckTeamIsHuman", 0, 0, 0) == true, "PickupSuccess")
			stop
		PickupSuccess:
			TNT1 A 0 A_GiveInventory("HuntingShotgun", 1)
			stop
	}
}

actor HuntingShotgun : ZHWeapon
{
	Tag "Hunting shotgun"
	Weapon.SlotNumber 3
	Weapon.AmmoGive 8
	Weapon.AmmoType "ShotgunShell"
	Weapon.AmmoType2 "ShotgunShell"
	weapon.Kickback 160

	+WEAPON.AMMO_OPTIONAL

	States
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory("ShotgunPump", 1, "CockNormal")
			SHTX A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			wait
		
		Deselect:
			SHTS CB 2
			SHTS A 1
			TNT1 A 0 A_Lower
			wait
		
		Select:
			TNT1 A 0 A_Raise
			TNT1 A 0 A_WeaponReady
			SHTS A 1
			SHTS BC 2
			goto Ready
		
		// Fire state
		Fire:
			// Failsafe and also just a way to prevent damaging after the round ended.
			TNT1 A 0 A_JumpIf(CallACS("ScriptGameStateIsEnding", 0, 0, 0) == true, "Ready")
			
			TNT1 A 0 A_JumpIfInventory("ShotgunShell", 1, 2)
			SHTX A 5 A_PlayWeaponSound("weapons/shotgun/EMPTY")
			Goto Reload
			TNT1 A 0 A_GiveInventory("ShotgunPump", 1)
			TNT1 A 0 Radius_Quake(2, 6, 0, 1, 0)
			TNT1 AAAAAA 0 A_FireCustomMissile("WeaponSmoke", Frandom(-8.0, 8.0), FALSE, 0, 0, CMF_AIMDIRECTION, Frandom(-8.0, 8.0))
			TNT1 A 0 A_TakeInventory("ShotgunShell", 1)
			TNT1 A 0 A_PlaySound ("weapons/shotgun/FIRE", CHAN_WEAPON)
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_FireBullets(5.0, 5.0, 8, 10, "BulletPuff", FBF_NOFLASH | FBF_NORANDOM | FBF_NORANDOMPUFFZ)
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_SetPitch(pitch-2.0)
			SHTX B 1 bright
			SHTX CD 1 A_SetPitch(pitch+1.0)
			SHTX DEFAA 1
			goto CockNormal
		
		// Shotgun cocking
		CockNormal:
			SHTX GGHHIIJJKK 1 A_WeaponReady(WRF_NOFIRE)
			TNT1 A 0 A_PlaySoundEx("weapons/shotgun/COCKDOWN", "SoundSlot5")
			SHTX L 1
			SHTX M 2
			SHTX N 3
			SHTX M 2
			TNT1 A 0 A_PlaySoundEx("weapons/shotgun/COCKUP", "SoundSlot5")
			SHTX L 1
			SHTX KJIHG 2
			SHTX A 0 A_TakeInventory("ShotgunPump", 1)
			goto Ready
		
		// Reload states
		Reload:
			TNT1 A 0 A_JumpIfInventory("ShotgunShell", 8, "Ready")
			SHTX HHIIJJ 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			SHTR A 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			goto ReloadLoop

		ReloadLoop:
			TNT1 A 0 A_JumpIfInventory("ShotgunShell", 8, "ReloadEnd")
			SHTR BBCC 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			TNT1 A 0 A_PlaySoundEx("weapons/shotgun/INSERT", "SoundSlot5")
			SHTR DDEE 1 A_WeaponReady(WRF_NOBOB)
			TNT1 A 0 A_GiveInventory("ShotgunShell",1)
			SHTR FFGGHHII 1 A_WeaponReady(WRF_NOBOB)
			loop

		ReloadEnd:
			TNT1 A 0 A_PlaySoundEx("weapons/shotgun/COCKDOWN", "SoundSlot5")
			SHTX L 1
			SHTX M 2
			SHTX N 3
			SHTX M 2
			TNT1 A 0 A_PlaySoundEx("weapons/shotgun/COCKUP", "SoundSlot5")
			SHTX L 1
			SHTX KJIHG 2
			goto Ready
	}
}