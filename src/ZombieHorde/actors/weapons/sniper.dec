
// If true, the sniper is still required to bolt.
actor SniperBolting : Boolean {}

// If true, then the sniper rifle shows a special graphic.
// This wont directly indicate the sniper is in a zoomed state.
actor IsZoomed : Boolean {}

// Indicates the zoom level that the sniper is in.
// This is also used to indicate that the sniper is zoomed between reloads
actor SniperZoomlevel : inventory { inventory.MaxAmount 2 }

actor ZHSniperSpawn : ZHweaponSpawn 8001
{
	inventory.PickupMessage "Sniper rifle"
	tag "Sniper rifle"
	
	states
	{
		// Spawn imitates a rocket launcher, because you're not supposed to see it anyway.
		Spawn:
			LAUN A -1
		
		Pickup:
			TNT1 A 0 A_JumpIf(CallACS("CheckTeamIsHuman", 0, 0, 0) == true, "PickupSuccess")
			stop
		PickupSuccess:
			TNT1 A 0 A_GiveInventory("ZHSniperRifle", 1)
			stop
	}
}

actor ZHSniperRifle : ZHWeapon
{
	tag "Sniper rifle"
	weapon.SlotNumber 5
	weapon.KickBack 1

	+WEAPON.NOAUTOFIRE
  
	States
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory("SniperBolting", 1, "SniperBolt")
			TNT1 A 0 A_JumpIfInventory("IsZoomed", 1, "ReadyZoomed")
		ReadyIdle:
			SNPR A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			wait

		ReadyZoomed:
			TNT1 A 1 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
			goto ReadyZoomed

		Deselect:
			TNT1 A 0 A_JumpIfInventory("IsZoomed", 1, "DeselectZoom")
			SNPB CB 2
			SNPB A 1
			TNT1 A 0 A_Lower
			wait

		DeselectZoom:
			TNT1 A 0 A_TakeInventory("SniperZoomlevel", 2)
			TNT1 A 0 A_TakeInventory("IsZoomed", 1)
			TNT1 A 0 A_PlaySoundEx("weapons/sniper/ZOOMOUT", "SoundSlot5")
			TNT1 A 3
			TNT1 A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
			SNTR CBA 1
			goto Deselect

		Select:
			TNT1 A 0 A_Raise
			TNT1 A 0 A_WeaponReady
			SNPB A 1
			SNPB BC 2
			goto Ready
		
		
		// Zoom levels
		AltFire:
			TNT1 A 0 A_JumpIfInventory("SniperZoomlevel", 2, "ZoomOut")
			TNT1 A 0 A_JumpIfInventory("SniperZoomlevel", 1, "Zoom2")
			
		Zoom1:
			SNPB D 1 A_ZoomFactor(2.0)
			SNTR ABC 1
			TNT1 A 0 A_SetCrosshair(99999) // Sure nobody has that many crosshairs?
			TNT1 A 0 A_GiveInventory("IsZoomed", 1)
			TNT1 A 0 A_GiveInventory("SniperZoomlevel", 1)
			TNT1 A 0 A_PlaySoundEx("weapons/sniper/ZOOMIN", "SoundSlot5")
			goto ReadyZoomed
		Zoom2:
			TNT1 A 0 A_GiveInventory("SniperZoomlevel", 1)
			TNT1 A 0 A_PlaySoundEx("weapons/sniper/ZOOMIN", "SoundSlot5")
			TNT1 A 0 A_ZoomFactor(6.0)
			goto ReadyZoomed
		
		ZoomOut:
			TNT1 A 0 A_PlaySoundEx("weapons/sniper/ZOOMOUT", "SoundSlot5")
			TNT1 A 3
			TNT1 A 0 A_TakeInventory("SniperZoomlevel", 2)
			TNT1 A 0 A_TakeInventory("IsZoomed", 1)
			TNT1 A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
			TNT1 A 0 A_SetCrosshair(0)
			SNTR CBA 1
			goto Ready
			

		Fire:
			// Failsafe and also just a way to prevent damaging after the round ended.
			TNT1 A 0 A_JumpIf(CallACS("ScriptGameStateIsEnding", 0, 0, 0) == true, "Ready")
			
			TNT1 A 0 A_PlaySound("weapons/sniper/FIRE", CHAN_WEAPON)
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 Radius_Quake(2, 6, 0, 1, 0)
			TNT1 A 0 A_GiveInventory("SniperBolting", 1)
			TNT1 A 0 A_JumpIfInventory("IsZoomed", 1, "FireZoomed")
			TNT1 AAAAAAAAA 0 A_FireCustomMissile("WeaponSmoke", Frandom(-10, 10.0), false, 6, 8, CMF_AIMDIRECTION, Frandom(-4.0, 4.0))
			TNT1 A 0 A_FireBullets(1, 1, 1, 250 + (50 * random(-1, 1)), "BulletPuff", FBF_NORANDOM)
			SNPR B 1 bright
			TNT1 A 0 A_SetPitch(pitch-3.0)
			SNPR C 3
			SNPR DD 1 A_SetPitch(pitch+1.0)
			SNPR E 1 A_SetPitch(pitch+1.0)
			goto SniperBolt
			
		// Firing while currently zoomed in.
		FireZoomed:
			TNT1 A 0 A_TakeInventory("IsZoomed", 1)
			TNT1 AAAAAAAAA 0 A_FireCustomMissile("WeaponSmoke", Frandom(-8.0, 8.0), false, 0, 4, CMF_AIMDIRECTION, Frandom(-8.0, 8.0))
			TNT1 A 0 A_FireBullets(1, 1, 1, 250 + (50 * random(-1, 1)), "BulletPuff", FBF_NORANDOM)
			TNT1 A 0 A_SetPitch(pitch-2.0)
			TNT1 A 0 A_SetAngle(angle-1.0)
			TNT1 A 3
			LDHR A 0 A_SetAngle(angle+1.0)
			TNT1 A 1 A_SetPitch(pitch+1.0)
			TNT1 A 1
			TNT1 A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
			SNTR CBA 1
			goto SniperBolt
			
		SniperBolt:
			SNPR FGHIJ 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
			SNPR KLM 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)

			TNT1 A 0 A_PlaySoundEx("weapons/sniper/UP","SoundSlot5")
			SNPR NNNOOO 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
			SNPR P 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
			TNT1 A 0 A_PlaySoundEx("weapons/sniper/BACK","SoundSlot5")
			SNPR PPP 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
			TNT1 A 0 A_TakeInventory("SniperBolting", 1)
			SNPR QQRR 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
			TNT1 A 0 A_PlaySoundEx("weapons/sniper/FORWARD","SoundSlot5")
			SNPR S 4 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
			SNPR TTUU 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
			SNPR HG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
			TNT1 A 0 A_JumpIfInventory("SniperZoomlevel", 1, "SniperBoltEndZoomed")
			goto Ready
		
		SniperBoltEndZoomed:
			SNTR ABC 1
			TNT1 A 0 A_GiveInventory("IsZoomed", 1)
			TNT1 A 0 A_JumpIfInventory("SniperZoomlevel", 2, "SniperBoltEndZoomedFullZoom")
			TNT1 A 0 A_ZoomFactor(2.0, ZOOM_INSTANT)
			goto Ready
		
		SniperBoltEndZoomedFullZoom:
			TNT1 A 0 A_ZoomFactor(6.0, ZOOM_INSTANT)
			goto Ready
	}
}