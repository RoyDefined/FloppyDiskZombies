actor DebugDrop
{
	Height 2
	Radius 2
	Renderstyle Translucent
	
	+MISSILE
	+CLIENTSIDEONLY
	+NOTIMEFREEZE
	+NOBLOCKMAP
	+DONTSPLASH
	+NOGRAVITY
	+FORCEYBILLBOARD
	
	States
	{
		Spawn:
			PUF2 A 35
			stop
	}
}

actor RainDrop
{
	Height 2
	Radius 2
	Gravity 2.0
	Renderstyle Translucent
	YScale .6
	XScale .2
	
	+MISSILE
	+CLIENTSIDEONLY
	+NOTIMEFREEZE
	+NOBLOCKMAP
	+DONTSPLASH
	+NOGRAVITY
	+FORCEYBILLBOARD
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_ChangeVelocity(0, 0, Frandom(-20.0, -30.0), CVF_REPLACE)
			TNT1 A 0 A_SetTranslucent(FRandom(0.3, 0.9))
			RAIN AAAAAAAAAAAA 1 A_JumpIf(WaterLevel > 0, "Death.Silent")
			
		// Remove the actor manually after a while.
		Spawn.FadeOut:
			RAIN A 1 A_JumpIf(WaterLevel > 0 ,"Death.Silent")
			TNT1 A 0 A_FadeOut(.01)
			loop
		
		Death:
			TNT1 A 0 A_SetTranslucent(0.1)
			TNT1 A 0 A_SetScale(0.3, 0.3)
			RAIN BCDEF 2
			RAIN GGHH 1 A_FadeOut(.15)
			stop
		
		// Death by water does not look good so we might aswell show nothing
		Death.Silent:
			TNT1 A 0
			stop
	}
}

actor SnowDrop
{
	Height 2
	Radius 2
	Renderstyle Translucent
	
	Scale 0.2
	
	+MISSILE
	+CLIENTSIDEONLY
	+NOTIMEFREEZE
	+NOBLOCKMAP
	+DONTSPLASH
	+NOGRAVITY
	+FORCEXYBILLBOARD
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_ChangeVelocity(Frandom(-1.0, 1.0), Frandom(-1.0, 1.0), Frandom(-4.0, -1.0), CVF_REPLACE)
			TNT1 A 0 A_SetTranslucent(FRandom(0.3, 0.9))
			SNOW AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_JumpIf(WaterLevel > 0, "Death")
		
		// Remove the actor manually after a while.
		Spawn.FadeOut:
			SNOW A 1 A_JumpIf(WaterLevel > 0, "Death")
			TNT1 A 0 A_FadeOut(.025)
			loop
		
		Death:
			SNOW A 1 A_FadeOut(.025)
			loop
	}
}

actor DustParticle
{
	Height 2
	Radius 2
	Renderstyle Translucent
	Alpha 0
	
	+MISSILE
	+CLIENTSIDEONLY
	+NOTIMEFREEZE
	+NOBLOCKMAP
	+DONTSPLASH
	+NOGRAVITY
	+FORCEXYBILLBOARD
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_ChangeVelocity(Frandom(-0.1, 0.1), Frandom(-0.1, 0.1), Frandom(-0.1, 0.1), CVF_REPLACE)
			TNT1 A 0 A_JumpIf(WaterLevel > 0, "Death.Silent")
			TNT1 A 0 A_SetScale(Frandom(0.05, 0.2), Frandom(0.05, 0.2))
			TNT1 A 0 A_Jump(192, 1, 2, 3)
			goto Spawn.FadeIn
			TNT1 B 0
			goto Spawn.FadeIn
			TNT1 C 0
			goto Spawn.FadeIn
			TNT1 D 0
			goto Spawn.FadeIn
			
		Spawn.FadeIn:
			DUST "####" 1 A_FadeIn(.1)
			TNT1 A 0 A_Jump(50, "Spawn.Rest")
			DUST "##" 1 A_FadeIn(.05)
			TNT1 A 0 A_Jump(50, "Spawn.Rest")
			DUST "##" 1 A_FadeIn(.05)
			TNT1 A 0 A_Jump(50, "Spawn.Rest")
			DUST "##" 1 A_FadeIn(.05)
		Spawn.Rest:
			DUST "###########" 5 A_JumpIf(WaterLevel > 0, "Death.Silent")
		
		// Remove the actor manually after a while.
		Spawn.FadeOut:
			DUST "#" 1 A_JumpIf(WaterLevel > 0,"Death.Silent")
			TNT1 "#" 0 A_FadeOut(.02)
			loop
		
		Death.Silent:
			TNT1 A 0
			stop
	}
}

actor SparkParticle
{
	Height 2
	Radius 2
	Renderstyle Add
	Alpha 0
	
	+MISSILE
	+CLIENTSIDEONLY
	+NOTIMEFREEZE
	+NOBLOCKMAP
	+DONTSPLASH
	+NOGRAVITY
	+FORCEXYBILLBOARD
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_ChangeVelocity(Frandom(-0.1, 0.1), Frandom(-0.1, 0.1), Frandom(-0.1, 0.1), CVF_REPLACE)
			TNT1 A 0 A_JumpIf(WaterLevel > 0, "Death.Silent")
			TNT1 A 0 A_Jump(192, 2, 3, 4)
			TNT1 A 0 A_SetScale(0.02, 0.02)
			goto Spawn.FadeIn
			TNT1 A 0 A_SetScale(0.01, 0.01)
			goto Spawn.FadeIn
			TNT1 A 0 A_SetScale(0.005, 0.005)
			goto Spawn.FadeIn
			TNT1 A 0 A_SetScale(0.002, 0.002)
			
		Spawn.FadeIn:
			SPK1 AAAA 1 A_FadeIn(.1)
			TNT1 A 0 A_Jump(50, "Spawn.Rest")
			SPK1 AA 1 A_FadeIn(.05)
			TNT1 A 0 A_Jump(50, "Spawn.Rest")
			SPK1 AA 1 A_FadeIn(.05)
			TNT1 A 0 A_Jump(50, "Spawn.Rest")
			SPK1 AA 1 A_FadeIn(.05)
		Spawn.Rest:
			SPK1 AAAAAAAAAAAAA 5 A_JumpIf(WaterLevel > 0, "Death.Silent")
		
		// Remove the actor manually after a while.
		Spawn.FadeOut:
			SPK1 A 1 A_JumpIf(WaterLevel > 0,"Death.Silent")
			TNT1 A 0 A_FadeOut(.02)
			loop
		
		Death.Silent:
			TNT1 A 0
			stop
	}
}