actor ToilerSpawner : RandomSpawner 20070
{
	//$Category "Decoration / Shootable"
	//$NotAngled
	//$Sprite TOLTA0
	tag "Toilet spawner"
	DropItem "Toilet1" 255, 3
	DropItem "BrokenToilet1" 255, 1
}

/*
** -----------------------------------------------------------------------------
** Pristine variant
** -----------------------------------------------------------------------------
*/

actor Toilet1
{
	health 50
	radius 10
	height 40
	mass 0x7FFFFFFF
	scale .8
	
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	
	states
	{
		Spawn:
			TOLT A -1
			
		Death:
			TNT1 A 0 A_SpawnItem("BrokenToilet1")
			TNT1 A 0 A_SpawnItem("BrokenToiletFX")
			TNT1 A 0 A_SpawnItem("BrokenToiletWater")
			TNT1 A 0 A_SpawnItem("ToiletPieceSpawner", 0, 30)
			Stop
	}
}

/*
** -----------------------------------------------------------------------------
** Broken variant
** -----------------------------------------------------------------------------
*/

actor BrokenToilet1
{
	radius 10
	height 40
	scale .8
	
	+SOLID
	
	States
	{
		Spawn:
			TOLT B -1
	}
}

/*
** -----------------------------------------------------------------------------
** particles
** -----------------------------------------------------------------------------
*/

actor BrokenToiletFX
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NOTIMEFREEZE
	states
	{
		spawn:
			TNT1 A 0 NoDelay A_PlaySound("decoration/toiletbreak")
			Stop
	}
}

actor ToiletPieceSpawner
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NOTIMEFREEZE
	
	states
	{
		spawn:
			TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("ToiletPiece", random(-10, 10), random(-10, 10), random(-10, 10), Frandom(-2.5, 2.5), Frandom(-2.5, 2.5), Frandom(0.5, 2.5), random(0, 359))
			stop
		NoSpawn:
			TNT1 A 0
			Stop
	}
}

actor ToiletPiece
{
	+MISSILE
	-NOGRAVITY
	+NOBLOCKMAP
	+THRUACTORS
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	+DONTSPLASH
	+NOTIMEFREEZE
	
	states
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetScale(Frandom(0.4, 1.4), Frandom(0.4, 1.4))
			TOIL DEFGHDEFGHDEFGHDEFGHDEFGHDEFGHDEFGHDEFGHDEFGHDEFGH 2 A_JumpIf(WaterLevel > 0, "underwater")
		DeSpawn:
			TOIL DEFGH 2 A_Fadeout(.4)
			loop
			
		// Underwater Fade out
		Underwater:
			TOIL "########" 2 A_ChangeVelocity(velx/1.4, vely/1.4, velz/1.1, CVF_REPLACE)
		UnderwaterEnd:
			TOIL "########" 2 A_Fadeout(.05)
			Goto UnderwaterEnd
			
		// Death fade out
		Death:
			TNT1 D 0 A_Jump(170, 1, 2)
			goto Death+3
			TNT1 E 0
			goto Death+3
			TNT1 G 0
			goto Death+3
			
			TNT1 "#" 0 A_SetTranslucent(1.0)
			TOIL "#" 0 A_SetTics(random(30, 80))
			
		// Fade out
			TOIL "#" 1 A_Fadeout(.01)
			Goto Death+5
	}
}

actor BrokenToiletWater
{
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	+FORCEYBILLBOARD
	+NOTIMEFREEZE
	scale 0.4
	alpha 0.2
	renderstyle Add
	var int user_var;
	
	states
	{
		Spawn:
			TNT1 A 0 NoDelay A_Jump(180, "NoSpawn")
			TNT1 A 0 A_PlaySound("decoration/waterfall", CHAN_BODY, 0.8, TRUE, ATTN_STATIC)
			TNT1 A 0 A_SetUserVar("user_var", user_var+1)
			TNT1 A 0 A_JumpIf(user_var >= 50, "NoSpawn")
			TNT1 A 0 A_SetScale(0.4 - 0.005*user_var, 0.4 - 0.005*user_var)
			CEFE A 2
			TNT1 AAA 0 A_SpawnItemEx("ToilerWaterParticle", random(-5, 5), random(-5, 5), random(0, 20), Frandom(-0.5, 0.5), Frandom(-0.5, 0.5), Frandom(4.0, 12.0) - 0.12*user_var)
			CEFE B 2
			TNT1 AAA 0 A_SpawnItemEx("ToilerWaterParticle", random(-5, 5), random(-5, 5), random(0, 20), Frandom(-0.5, 0.5), Frandom(-0.5, 0.5), Frandom(4.0, 12.0) - 0.12*user_var)
			CEFE C 2
			TNT1 AAA 0 A_SpawnItemEx("ToilerWaterParticle", random(-5, 5), random(-5, 5), random(0, 20), Frandom(-0.5, 0.5), Frandom(-0.5, 0.5), Frandom(4.0, 12.0) - 0.12*user_var)
			goto Spawn+2
		NoSpawn:
			TNT1 A 0 A_StopSound(CHAN_BODY)
			stop
	}
}

actor ToilerWaterParticle
{
	+MISSILE
	-NOGRAVITY
	+THRUACTORS
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	+BOUNCEONWALLS
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	Alpha 0.4
	Radius 1
	Height 1
	Renderstyle Add
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetScale(Frandom(0.1, 0.4), Frandom(0.1, 0.4))
			TNT1 A 0 A_Jump(170, 1, 2)
			Goto Spawn+4
			TNT1 B 0
			Goto Spawn+4
			TNT1 C 0
			
			W3PH "######################################" 1 A_JumpIf(WaterLevel > 0, "Death")

		Death:
			W3PH "#" 1 A_FadeOut(.1)
			loop
	}
}